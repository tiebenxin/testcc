apply plugin: 'com.android.application'

android {
    def signingProperties = "signing.properties"
    signingConfigs {
        def signingFile = file("${signingProperties}");
        if (signingFile.exists()) {
            Properties props = new Properties()
            props.load(new FileInputStream(signingFile))

            if (props.containsKey("keyStore") && props.containsKey("keyStorePassword") &&
                props.containsKey("keyAlias") && props.containsKey("keyAliasPassword")) {
                release {
                    storeFile = file(props.keyStore)
                    storePassword = props.keyStorePassword
                    keyAlias = props.keyAlias
                    keyPassword = props.keyAliasPassword
                }
            }
        }
    }

    compileSdkVersion 26
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]
    defaultConfig {
        applicationId rootProject.ext.android["applicationId"]
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
        multiDexEnabled true

        ndk {
            //选择要添加的对应cpu类型的.so库。
            moduleName = "MediaEncode"
            moduleName = "dc"
            abiFilters 'armeabi-v7a'
            cFlags "-std=c11 -Wall -Wextra -Os -fno-stack-protector"
        }

        /**
         * 维度统一
         */
        flavorDimensions "client"
        /**
         * common
         */
        resValue("string", "app_name", "飞鸽互联_Debug")
    }
    buildTypes {
        /**
         * debug
         * --面向开发者
         */
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.release
            buildConfigField("String", "CHANNEL", "\"debug\"")
            manifestPlaceholders = [
                "LAUNCHER_ACTIVITY" : "com.lensim.fingerchat.fingerchat.ui._SplashActivity"]
        }
        /**
         * beta
         * -- 面向测试小伙伴
         */
        beta {
            debuggable true
            zipAlignEnabled false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue("string", "app_name", "飞鸽互联_Beta")
            buildConfigField("String", "CHANNEL", "\"beta\"")
            manifestPlaceholders = [
                "LAUNCHER_ACTIVITY" : "com.lensim.fingerchat.fingerchat.ui._SplashActivity"]
        }
        /**
         * release
         * 面向正式用户
         */
        release {
            debuggable true
            zipAlignEnabled false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue("string", "app_name", "飞鸽互联")
            buildConfigField("String", "CHANNEL", "\"\"")
            manifestPlaceholders = [
                "LAUNCHER_ACTIVITY" : "com.lensim.fingerchat.fingerchat.ui.SplashActivity"]
        }
    }
    productFlavors {
        Lens {
            // TODO
            manifestPlaceholders.put("CHANNEL", name)
        }
    }

    /**
     * 重命名apk
     */
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (outputFile != null &&
                outputFile.name.endsWith('.apk') &&
                'release'.equals(variant.buildType.name)) {
                outputFileName = "${variant.productFlavors[0].name}_v${variant.versionName}_${buildTime()}.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    sourceSets {
        debug.setRoot('buildtypes/debug')
        beta.setRoot('buildtypes/beta')
    }
}

def buildTime() {
    def date = new Date()
    def formatDate = date.format("yyyyMMddHHmmss", TimeZone.getTimeZone("UTC"))
    return formatDate
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    compile project(path: ':zxingSdk')
    implementation project(path: ':fingerchat-sdk')
    implementation project(path: ':commons')
    implementation project(path: ':components')
    implementation project(path: ':data')
    implementation project(path: ':db')
    implementation project(path: ':chatmodel')
    implementation project(path: ':WebView')
    implementation project(path: ':librarys:annotation')
    implementation project(path: ':hexmeet')
    implementation rootProject.ext.dependencies["design"]
    implementation rootProject.ext.dependencies["support-v4"]
    implementation rootProject.ext.dependencies["cardview-v7"]
    implementation rootProject.ext.dependencies["recyclerview"]
    implementation "com.android.support:multidex:$rootProject.ext.multidexVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.ext.constraintVersion"
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    implementation "com.jakewharton:butterknife:$rootProject.ext.butterknifeVersion"
    implementation "com.github.hackware1993:MagicIndicator:$rootProject.ext.magicIndicatorVersion"
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    implementation 'com.tencent.sonic:sdk:2.0.0'
    //图片裁剪
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'me.jessyan:progressmanager:1.3.3'
    implementation 'com.github.chrisbanes:PhotoView:1.3.0'
    implementation 'com.zhy:percent-support-extends:1.1.1'
    // leakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    // stetho
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    implementation "com.google.guava:guava:$rootProject.ext.guavaVersion"
    api "com.google.guava:guava:$rootProject.ext.guavaVersion"
}
